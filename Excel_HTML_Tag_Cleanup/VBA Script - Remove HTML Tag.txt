Private Sub btnRemoveTags_Click()

    ' Declare basic set of variables to perform the HTML Tag Cleanup in Scanned range
    Dim ws As Worksheet
    Dim rng As Range
    Dim cell As Range
    Dim regEx As Object
    Dim strInput As String
    Dim strOutput As String

    ' Set the worksheet variable to the active sheet
    Set ws = ActiveSheet
    
    ' Establish the data range with the used range object
    ' The UsedRange in VBA is a worksheet property that returns a range object representing the range used (all Excel cells used or filled in a worksheet) on a particular worksheet.
    Set rng = ws.UsedRange
    
    ' Create the regular expression object
    ' Set the pattern REG-EXP-OBJ to refer to HTML tags
    Set regEx = CreateObject("VBScript.RegExp")
    With regEx
        .Global = True
        .MultiLine = True
        .IgnoreCase = True
        .Pattern = "<.*?>"
    End With

    ' Loop through each cell in the declared data range variable
    ' During the loop the REG-EXP-OBJ is called to perform the required Clean-Up of HTML Tags
    
    For Each cell In rng
        strInput = cell.Value
        If regEx.test(strInput) Then
            strOutput = regEx.Replace(strInput, "")
            cell.Value = strOutput
        End If
    Next cell

    ' Clean up the established values
    Set regEx = Nothing
    Set rng = Nothing
    Set ws = Nothing

End Sub
